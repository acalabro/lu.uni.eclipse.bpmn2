# 1. What are the duties of the data controller?
SELECT DISTINCT ?x ?duty
WHERE {
	?x a <http://www.w3.org/2002/07/owl#Class> .
	?x <http://www.w3.org/2000/01/rdf-schema#subClassOf>* <http://www.uni.lu/dataprotection#OWLClass_af4ae221_0c41_4b82_8ff9_7561e9d9c6cd> .
	?x <http://www.w3.org/2000/01/rdf-schema#label> ?duty .
}

# 2. What are the functions of the processor?
SELECT DISTINCT ?x ?function ?y ?target
WHERE {
	?x a <http://www.w3.org/2002/07/owl#ObjectProperty> .
	?x <http://www.w3.org/2000/01/rdf-schema#label> ?function .
	?x <http://www.w3.org/2000/01/rdf-schema#domain> <http://www.uni.lu/dataprotection#OWLClass_3c5113eb_0923_4946_86cf_f5a83de7f9bb> .
	?x <http://www.w3.org/2000/01/rdf-schema#range> ?y .
	?y <http://www.w3.org/2000/01/rdf-schema#label> ?target .
}

# 3. What are the rights of the data subject?
SELECT DISTINCT ?x ?right
WHERE {
	?x a <http://www.w3.org/2002/07/owl#Class> .
	?x <http://www.w3.org/2000/01/rdf-schema#subClassOf>* <http://www.uni.lu/dataprotection#OWLClass_8fd1b241_6e73_4771_9cf7_4a995dd8311b> .
	?x <http://www.w3.org/2000/01/rdf-schema#label> ?right .
}

# 4. How do the data subject's rights relate to the data processing requirements?
SELECT DISTINCT ?x ?action ?z ?requirement
WHERE {
	?x a <http://www.w3.org/2002/07/owl#Class> .
	?x <http://www.w3.org/2000/01/rdf-schema#label> ?action .
	?x <http://www.w3.org/2002/07/owl#equivalentClass> [
		<http://www.w3.org/2002/07/owl#onProperty> <http://www.uni.lu/dataprotection#OWLObjectPropertyImpl_9679a244_6762_4da7_9aaa_c89f96e6f22c> ;
		<http://www.w3.org/2002/07/owl#someValuesFrom> ?y ] .
	?y a <http://www.w3.org/2002/07/owl#Class> .
	?y <http://www.w3.org/2000/01/rdf-schema#subClassOf> ?z .
	?z a <http://www.w3.org/2002/07/owl#Class> .
	?z <http://www.w3.org/2000/01/rdf-schema#label> ?requirement .
}

# 5. How can a data subject enforce their rights against a data controller?
SELECT DISTINCT ?y ?action ?x ?right
WHERE {
	?x a <http://www.w3.org/2002/07/owl#Class> .
	?x <http://www.w3.org/2000/01/rdf-schema#label> ?right .
	?x <http://www.w3.org/2002/07/owl#equivalentClass> ?c .
	?c <http://www.w3.org/2002/07/owl#onProperty> <http://www.uni.lu/dataprotection#OWLObjectPropertyImpl_9679a244_6762_4da7_9aaa_c89f96e6f22c> .
	?c <http://www.w3.org/2002/07/owl#someValuesFrom> ?y .
	?y a <http://www.w3.org/2002/07/owl#Class> .
	?y <http://www.w3.org/2000/01/rdf-schema#label> ?action .
}

# 7. Who is in charge of overseeing the controller's activities?
# Coverage: 1 objectProperty
SELECT DISTINCT ?x ?overseer
WHERE {
	?x a <http://www.w3.org/2002/07/owl#Class> .
	<http://www.uni.lu/dataprotection#OWLObjectPropertyImpl_4bf9eaca_1040_4861_8e69_2eb6cfe40e83> <http://www.w3.org/2000/01/rdf-schema#domain> ?x .
	?x <http://www.w3.org/2000/01/rdf-schema#label> ?overseer .
}

# Who is reponsible for appointing the Data Protection Officer?
# Coverage: 1 objectProperty
SELECT DISTINCT ?x ?appointer
WHERE {
	?x a <http://www.w3.org/2002/07/owl#Class> .
	<http://www.uni.lu/dataprotection#OWLObjectProperty_16ef80bb_336f_4678_b647_aa3ce5c02e49> <http://www.w3.org/2000/01/rdf-schema#domain> ?x .
	?x <http://www.w3.org/2000/01/rdf-schema#label> ?appointer .
}

# What are the actions that a data subject can perform with respect to data processing?
SELECT DISTINCT ?x ?action
WHERE {
	?x a <http://www.w3.org/2002/07/owl#Class> .
	?x <http://www.w3.org/2000/01/rdf-schema#subClassOf>* <http://www.uni.lu/dataprotection#OWLClassImpl_58cf8e1b_e912_4f83_bdbe_0214e093bd4c> .
	?x <http://www.w3.org/2000/01/rdf-schema#label> ?action .
}

# What are the duties of the data controller that do not concern the transfer?
SELECT DISTINCT ?x ?duty
WHERE {
	?x a <http://www.w3.org/2002/07/owl#Class> .
	?x <http://www.w3.org/2000/01/rdf-schema#subClassOf>* <http://www.uni.lu/dataprotection#OWLClass_af4ae221_0c41_4b82_8ff9_7561e9d9c6cd> .
	?x <http://www.w3.org/2000/01/rdf-schema#label> ?duty
	MINUS {
		?x a <http://www.w3.org/2002/07/owl#Class> .
		?x <http://www.w3.org/2000/01/rdf-schema#subClassOf>* <http://www.uni.lu/dataprotection#OWLClass_06a52543_55ee_4037_8370_dd56a165fe70> .
		?x <http://www.w3.org/2000/01/rdf-schema#label> ?duty .
	} .
}
